//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'org.graalvm.buildtools.native' version '0.9.27'
}

group = rootProject.group
version = rootProject.version

tasks.withType(JavaCompile).configureEach {
    outputs.upToDateWhen { false }
    options.encoding = "UTF-8"
    options.release.set 17
}

repositories.mavenCentral()
dependencies.compileOnly 'org.jetbrains:annotations:24.0.0'

jar {
    manifest.attributes([
            'Main-Class'              : 'dev.rdh.imag.launcher.Main',
            'Implementation-Title'    : 'imag-launcher',
            'Implementation-Version'  : project.version.split { '.build-' }[0],
            'Implementation-Vendor-Id': 'dev.rdh',
            'Implementation-Vendor'   : 'rdh',
    ])
}

graalvmNative {
    testSupport = false

    binaries.configureEach {
        resources.autodetect()
    }
    toolchainDetection = false

    binaries {
        main {
            imageName = 'imag'
            mainClass = 'dev.rdh.imag.launcher.Main'

            def args = [
                    '--verbose', '--no-fallback',
                    //'-Dgraal.CompilerConfiguration=enterprise',
                    //'-Dgraal.TuneInlinerExploration=0'
            ]
            args.forEach(buildArgs::add)

            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(17)
                vendor = JvmVendorSpec.matching('GraalVM Community')
            }
        }
    }
}